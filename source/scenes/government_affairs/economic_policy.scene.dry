title: Economic Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: economic_policy_timer += 12; nationalize_budget = 4; nationalize_budget -= 1 if works_councils >= 2; nationalize_budget -= 1 if works_councils >= 3; nationalize_budget -= 1 if socializations + nationalization_progress >= 2; wtb_budget = 4; wtb_budget -= 1 if works_councils >= 2 or socializations >= 1 or pro_labor >= 2
view-if: spd_in_government = 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and economic_policy_timer <= 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg
#subtitle: Policies to address the economic crisis.

= Economic Policy

We have already formulated an economic plan. Now we have to implement it. [? if budget < 0: If our budget falls too far into the red, a capital strike will almost certainly be triggered. ?]

- @wtb_2
- @wtb_2_deficit
- @wtb_continuation
- @implement_wtb_no_deficit
- @implement_wtb_deficit
- @nationalize_1
- @nationalize_2
- @moderate_plan
- @moderate_plan_limited_budget
- @root: Do not implement the economic plan.
- @easy_discard

@wtb_2
title: Continue the implementation of the WTB-Plan.
subtitle: -3 budget
unavailable-subtitle: Unemployment is low enough that the time for the WTB-Plan has passed.
view-if: wtb_implemented > 2 and budget >= 3 
choose-if: unemployed > 8
on-arrival: works_program += 1; budget -= 3; workers_spd += 7*(1-dissent); workers += 2 if unemployed > 8; unemployed -= 3 if unemployed > 8; unemployed_spd += 7*(1-dissent); workers_nsdap -= 3; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; dvp_left += 1; ddp_left += 3; ddp_cohesion += 1; lvp_left += 2; pro_republic += 3; economic_growth += 1.6; economic_growth -= 0.6 if economic_growth > 5

[? if works_program == 4 : We have commenced Phase 2 of the WTB-Plan. ?][? if works_program > 4 :  We have continued our implementation of the WTB-Plan. ?] In addition to public works, we are now focusing on rebuilding our industries.


@wtb_2_deficit
title: Continue the implementation of the WTB-Plan.
subtitle: -3 budget - we will enter a deficit.
unavailable-subtitle: Unemployment is low enough that we should not do deficit spending.
view-if: wtb_implemented > 2 and budget < 3 and budget >= -3
choose-if: unemployed > 10
on-arrival: works_program += 1; budget -= 3; workers_spd += 6*(1-dissent); workers += 2; unemployed -= 3 if unemployed > 8; unemployed_spd += 6*(1-dissent); workers_nsdap -= 2; coalition_dissent += 1; coalition_dissent -= 1 if z_leader == "Kaiser"; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; dvp_right += 1; ddp_right += 1; ddp_cohesion -= 1; lvp_right += 1; economic_growth += 1.6; economic_growth -= 0.6 if economic_growth > 5

We have continued our implementation of the WTB-Plan, despite the continued deficit spending. In addition to public works, we are now focusing on rebuilding our industries.


@wtb_continuation
title: Look back on our results, and continue our implementation...
view-if: wtb_implemented >= 1 and wtb_implemented <= 2
on-arrival: budget += 1 if works_program <= 2; unemployed -= 5 if (works_program <= 2 and unemployed > 15) else unemployed -= 3; unemployed += 3 if unemployed < 5; unemployed += 3 if unemployed < 0; workers += 4 if works_program <= 2 else workers += 2; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_nsdap -= 5; works_program += 1; z_relation += 3; dvp_relation += 3; lvp_relation += 3; coalition_dissent -= 1 if coalition_dissent >= 1; wtb_implemented += 1; capital_strike_progress -= 1 if capital_strike_progress >= 3; inflation += 1.5; inflation += 1 if unemployed < 10; dvp_left += 1; ddp_relation += 5; ddp_left += 3; ddp_cohesion += 2; lvp_left += 2; pro_republic += 5; economic_growth += 1; economic_growth -= 0.5 if economic_growth > 5

Our spending on public works has resulted in a lower need for unemployment insurance and welfare, and increased tax receipts through the multiplier effect as the new money courses through the economy, recuperating some of our spending. We continue to invest most of the new revenue into the program.

The economy is doing better, finally. Even the bourgeois parties are beginning to see the light.

[? if in_popular_front or in_left_front: The <span style="color: #700000;">**KPD**</span>, however, still opposes our policy of "rescuing" capitalism. ?]

@implement_wtb_no_deficit
title: Implement the WTB plan.
subtitle: -[+ wtb_budget +] budget. [? if wtb_budget == 3 : Our pro-labor policies have reduced the cost of the plan. ?][? if in_grand_coalition and dvp_ideology == "Right" and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> will immediately call a vote of no confidence. ?][? if in_weimar_coalition or in_grand_coalition and lvp_ideology == "Right" and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> will immediately call a vote of no confidence. ?]
unavailable-subtitle: [? if unemployed < 8: Unemployment is low enough that the time for the WTB-Plan has passed.?] [? if in_grand_coalition and (finance_minister_party == "DVP" or economic_minister_party == "DVP" and dvp_ideology != "Left") and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> will obstruct us using their position in the [? if finance_minister_party == "DVP": Finance Ministry?][? if economic_minister_party == "DVP" and finance_minister_party == "DVP":&nbsp;and?][? if economic_minister_party == "DVP": Economic Ministry?]. ?][? if (in_weimar_coalition or in_grand_coalition) and (finance_minister_party == "LVP" or economic_minister_party == "LVP" and lvp_ideology != "Left") and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> will obstruct us using their position in the [? if finance_minister_party == "LVP": Finance Ministry?][? if economic_minister_party == "LVP" and finance_minister_party == "LVP":&nbsp;and?][? if economic_minister_party == "LVP": Economic Ministry?]. ?]
view-if: budget >= wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and ((((economic_minister_party != "DVP" and finance_minister_party != "DVP") or dvp_ideology == "Left") or not in_grand_coalition) and not lvp_formed) or ((((economic_minister_party != "LVP" and finance_minister_party != "LVP") or lvp_ideology == "Left") or (not in_grand_coalition and not in_weimar_coalition)) and lvp_formed)
on-arrival: budget -= wtb_budget; coalition_dissent += 1 if (in_grand_coalition or in_minority_government); works_program += 1; unemployed -= 4; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; dvp_right += 1; ddp_left += 1; ddp_cohesion -= 1; lvp_right += 1; pro_republic += 3*(1-dissent); economic_growth += 2.8; 
go-to: let_it_happen if in_grand_coalition and dvp_ideology == "Right" and not lvp_formed; let_it_happen if in_weimar_coalition or in_grand_coalition and lvp_ideology == "Right" and lvp_formed
achievement: wirtschaftspolitik

We have started to enact the WTB plan. Money is beginning to flow through the economy via investments in large-scale public works. The bourgeois parties express concerns about inflation and spending, preferring tax cuts to address the economic crisis. However, they begrudgingly accept our plan, reassured by the surplus we’ve built up. [? if in_grand_coalition and dvp_ideology == "Right" and not lvp_formed or in_grand_coalition and lvp_ideology == "Right" and lvp_formed: except for the <span style="color: #C0A054;">**right liberals**</span> in our coalition, who immediately call a vote of no confidence. They refuse to be persuaded otherwise. ?]

[? if in_popular_front or in_left_front: The <span style="color: #700000;">**KPD**</span> opposes the WTB plan, seeing it as a rescue of capitalism rather than a step towards real <span style="color: #9b0000;">socialism</span>. ?]

@let_it_happen
on-arrival: time_to_election = 3; spd_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; vice_chancellor = "Dietrich"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann" if not stresemann_dead; foreign_minister = "Curtius" if stresemann_dead; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener"; coalition_dissent = 0; pro_republic -= 4; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50
go-to: let_it_happen_hindenburg if president != "Braun"; let_it_happen_braun if president == "Braun"

@let_it_happen_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; welfare -= 2; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930
go-to: elections if bruning_event_seen or year < 1930

The vote succeeds, and our government has resigned. Chancellor Brüning has been appointed as the head of a new cabinet, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

[? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

@elections
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_braun
on-arrival: spd_caretaker = 1
call: set_next_election_time

The vote succeeds, and our government has resigned. Chancellor [+ chancellor +] remains as the head of a caretaker government, but no longer has the power to enact new policies.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@implement_wtb_deficit
title: Implement the WTB plan.
subtitle: -[+ wtb_budget +] budget - we will enter deficit spending. [? if wtb_budget == 3 : Our pro-labor policies have reduced the cost of the plan. ?]
unavailable-subtitle: [? if unemployed < 8: Unemployment is low enough that the time for the WTB-Plan has passed.?] [? if in_grand_coalition and (finance_minister_party == "DVP" or economic_minister_party == "DVP") and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> will obstruct us using their position in the [? if finance_minister_party == "DVP": Finance Ministry?][? if economic_minister_party == "DVP" and finance_minister_party == "DVP":&nbsp;and?][? if economic_minister_party == "DVP": Economic Ministry?]. ?][? if (in_grand_coalition or in_weimar_coalition) and (finance_minister_party == "LVP" or economic_minister_party == "LVP") and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> will obstruct us using their position in the [? if finance_minister_party == "LVP": Finance Ministry?][? if economic_minister_party == "LVP" and finance_minister_party == "LVP":&nbsp;and?][? if economic_minister_party == "LVP": Economic Ministry?]. ?] [? if in_grand_coalition and dvp_ideology != "Left" and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> will call a preemptive vote of no confidence. ?][? if (in_grand_coalition or in_weimar_coalition) and lvp_ideology != "Left" and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> will call a preemptive vote of no confidence. ?]
view-if: budget < wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and (((economic_minister_party != "DVP" and finance_minister_party != "DVP" and (dvp_ideology == "Left" and in_grand_coalition) or not in_grand_coalition) and not lvp_formed) or ((economic_minister_party != "LVP" and finance_minister_party != "LVP" and (lvp_ideology == "Left" and (in_grand_coalition or in_weimar_coalition)) or (not in_grand_coalition and not in_weimar_coalition)) and lvp_formed))
on-arrival: coalition_dissent += 2; coalition_dissent += 1 if in_minority_government; coalition_dissent += 1 if chancellor == "Brüning"; coalition_dissent -= 1 if z_leader == "Kaiser"; works_program += 1; budget -= wtb_budget; unemployed -= 4; workers += 3; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; dvp_right += 3; ddp_right += 1; ddp_cohesion -= 1; dvp_relation -= 10; ddp_relation -= 5; z_relation -= 5; lvp_relation -= 8; lvp_right += 2; pro_republic += 3*(1-dissent); economic_growth += 3;
achievement: wirtschaftspolitik

We have entered the realm of deficit spending. Our coalition partners are disgusted, but the works plan is enacted anyway. Money is beginning to flow through the economy via investments in large-scale public works.

[? if in_popular_front or in_left_front: The <span style="color: #700000;">**KPD**</span> opposes the WTB plan, seeing it as a rescue of capitalism rather than a step towards real <span style="color: #9b0000;">socialism</span>. ?] 


####################### left economic policies (socializations)

@nationalize_1
title: Implement the Left plan for the transformation of the economy.
subtitle: -[+ nationalize_budget +] budget. [? if works_councils >= 3 : Our works councils have reduced the budget necessary for this program. ?] [? if socializations >= 2: Our existing socialization policies have reduced the budget necessary for this program. ?]
view-if: nationalization_adopted == 1 and nationalization_progress == 0
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget


@nationalize_no_deficit
on-arrival: nationalization_progress += 1; capital_strike_progress += 2; unemployed -= 3.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; workers += 2.5; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 1; dvp_relation -= 4; lvp_relation -= 4; left_dissent -= 5; new_middle_spd -= 2; old_middle_spd -= 3; economy_goal_completed += 1; inflation += 2; dvp_right += 2; ddp_relation -= 4; ddp_right += 3; ddp_cohesion -= 2; lvp_right += 2; goal_spd_cancel_peoples += 1; economic_growth += 1.7 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if nationalization_progress == 1 : We have begun purchasing idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress == 1 : The Left argues that this should only be a first step in the <span style="color: #c00000;">socialist</span> transformation of the economy. Eventually, much of the economy must be brought under popular control. ?]

[? if nationalization_progress > 1 : We purchase additional underutilized enterprises and run them in a coordinated <span style="color: #c00000;">socialist</span> fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@nationalize_deficit

Unfortunately, we do not have the budget to begin buying out industrial facilities. Some are urging for deficit spending in order to purchase factories, while others are encouraging (currently illegal) factory takeovers by workers, or nationalization without compensation. Taking any of these actions would require a vast departure from our previous party stances.

- @nationalize_deficit_1
- @nationalize_without_compensation
- @empower_workers
- @return: No, we should not do this.


@nationalize_deficit_1
title: Take a budget deficit to fund the nationalization program.
choose-if: left_strength > reformist_strength + center_strength or woytinsky_advisor == 1
subtitle: [? if woytinsky_advisor: Woytinsky can bring us around to supporting a budget deficit. ?]
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: The party does not yet support budget deficits for this purpose. ?] [? if not woytinsky_advisor : Perhaps bringing on an advisor might help make this action palatable? ?]
on-arrival: nationalization_progress += 1; capital_strike_progress += 3; unemployed -= 3 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 6; lvp_relation -= 6; z_relation -= 4; new_middle_spd -= 3; old_middle_spd -= 3; economy_goal_completed += 1; inflation += 3; dvp_right += 4; ddp_relation -= 6; ddp_right += 3; ddp_cohesion -= 2; lvp_right += 4; goal_spd_cancel_peoples += 1; economic_growth += 2 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if woytinsky_advisor: Most of the party, even on the left, are opposed to budget deficits for any purpose, with the exception of Wladimir Woytinsky. While he did not originally support the left's "transformational" plan, he has come around, and has persuaded the party and the Finance Ministry to support budget deficits in limited cases, for the purpose of exiting from the depression. ?]

[? if nationalization_progress == 1 : We have begun purchasing idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress == 1 : The Left argues that this should only be a first step in the <span style="color: #c00000;">socialist</span> transformation of the economy. Eventually, much of the economy must be brought under popular control. ?]

[? if nationalization_progress > 1 : We purchase additional underutilized enterprises and run them in a coordinated <span style="color: #c00000;">socialist</span> fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@nationalize_without_compensation
title: Nationalize without compensation.
subtitle: There is no reason why we should not simply take over idle factories. This is likely to provoke a confrontation with the capitalists.
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: The bulk of the party does not support this quasi-legal action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?]
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 3
on-arrival: nationalization_progress += 1; capital_strike_progress += 7; coup_progress += 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3 and unemployed >= 3; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; lvp_relation -= 10; left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 8; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 4; dvp_right += 5; ddp_right += 5; ddp_cohesion -= 2.5; lvp_right += 5; goal_spd_cancel_peoples += 1; economic_growth += 1 if economic_growth < 4;
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of nationalization without compensation. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed by keeping their factories idle, and we must take radical action.

[? if nationalization_progress == 1 : We have begun acquiring idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress > 1 : We acquired additional underutilized enterprises and run them in a coordinated <span style="color: #c00000;">socialist</span> fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@empower_workers
title: Empower workers to take over factories.
subtitle: We can promise to legalize factory occupations, giving the factories over to worker unions. This is highly confrontational, and would likely provoke a reaction from right-wing militants as well as capitalists.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the support of the works councils first. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; capital_strike_progress += 6; coup_progress += 5; unemployed -= 3 if unemployed >= 6; unemployed -= 1.5 if works_councils >= 3 and unemployed > 4; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; lvp_relation -= 10; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 10; factory_takeovers += 1; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 3; dvp_right += 10; ddp_right += 7.5; ddp_cohesion -= 3; lvp_right += 10; goal_spd_cancel_peoples += 1; economic_growth += 1 if economic_growth < 3;
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of legalizing factory occupations. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed, and we must take radical action.

<span style="color: #7A3C00;">Fascists</span> have been taking increasingly violent actions around the occupied factories, calling us thieves and looters. However, the some of the working-class supporters of the <span style="color: #7A3C00;">fascists</span> have been peeled off, or at least the ones with a sense of class solidarity.


@nationalize_2
title: Continue the Left plan for the transformation of the economy.
subtitle: -[+ nationalize_budget +] budget. [? if works_councils >= 3 : Our works councils have reduced the budget necessary for this program. ?] [? if socializations + nationalization_progress >= 2: Our existing socialization policies have reduced the budget necessary for this program. ?]
view-if: nationalization_adopted == 1 and nationalization_progress >= 1
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget

#######################################
# Moderate economic plan

@moderate_plan
title: Implement the Reformists' job-creation plan.
subtitle: -2 budget.
unavailable-subtitle: -2 budget. [? if budget < 2 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: budget -= 2; works_program += 1; unemployed -= 3 if unemployed >= 8; inflation += 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; dvp_left += 2; ddp_left += 1; ddp_cohesion += 0.5; lvp_left += 2; economy_goal_spd_peoples += 1; economic_growth += 1.6;
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : We have started to implement the job-creation plan! Funds are sent to build public works projects across Germany, reducing unemployment. ?]

[? if moderate_plan_progress > 1 : We have continued to fund the job-creation plan, using our funds to build public works throughout Germany. ?]

[? if unemployed >= 10 : The bourgeois approve of this plan as the "least bad" alternative, seeing it as necessary to save Germany's economy. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]

[? if in_popular_front or in_left_front: The <span style="color: #700000;">**KPD**</span> opposes the economic plan, seeing it as a rescue of capitalism rather than a step towards real <span style="color: #9b0000;">socialism</span>. ?]

@moderate_plan_limited_budget
title: Partially implement the Reformists' job-creation plan, with a lower cost.
subtitle: -1 budget.
unavailable-subtitle: -1 budget. [? if budget < 1 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 1 and unemployed >= 8
on-arrival: budget -= 1; works_program += 1; unemployed -= 2 if unemployed >= 8; inflation += 1; workers_spd += 4*(1-dissent); unemployed_spd += 2*(1-dissent); new_middle_spd += 2*(1-dissent); old_middle_spd += 1*(1-dissent); rural_spd += 1*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; dvp_left += 1; ddp_left += 0.5; lvp_left += 1; economy_goal_spd_peoples += 1; economic_growth += 0.7;
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : We have started to implement the job-creation plan! Funds are sent to build public works projects across Germany, reducing unemployment. ?]

[? if moderate_plan_progress > 1 : We have continued to fund the job-creation plan, using our funds to build public works throughout Germany. ?]

[? if unemployed >= 10 : The bourgeois approve of this plan as the "least bad" alternative, seeing it as necessary to save Germany's economy. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]

[? if in_popular_front or in_left_front: The <span style="color: #700000;">**KPD**</span> opposes the economic plan, seeing it as a rescue of capitalism rather than a step towards real <span style="color: #9b0000;">socialism</span>. ?]
