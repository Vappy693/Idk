title: Labor Unrest
subtitle: Clashes between workers and employers in the Ruhr...
view-if: year = 1928 and month = 12 and spd_in_government and labor_minister_party != "SPD" and z_leader != "Marx"
max-visits: 1
new-page: true
face-image: img/Kollwitz_Weavers.jpg
tags: event

= Labor Unrest

The mining and metals industry of the Ruhr region is beset by a wave of strikes against cuts to wages and benefits. Meanwhile, employers have started a retaliatory lockout to force their workers to accept these cuts. Both the strikers and the employers look to the <span style="color: #c00000;">**SPD**</span> in the government to resolve this crisis.

- @support_labor: Support labor in their demands.
- @support_employers: Support the employers in their demands.
- @balance: Try to strike a compromise between the sides.

@support_labor
subtitle: We don't own the Labor Ministry.
go-to: support_labor_failed if (in_grand_coalition or z_leader != "Joos"); negotiations

@negotiations
on-arrival: z_relation -= 2

Labor Minister [+ labor_minister +] (**<span style="color: #000000;">**Z**</span>**) was in the process of negotiating a compromise, and is deeply annoyed by our aggitation. However, with him and the newly elected <span style="color: #000000;">Center Party</span> leader Joseph Joos both being trade unionists, and Joos desire to maintain a coalition with us, perhaps they can be convinced to reconsider?

- @resources: use party resources to convince the Zentrum
- @Braun_pressure: Have chancellor Braun pressure them into agreeing
- @Tried: At least we tried.

@resources
subtitle: -2 resources
choose-if: z_relation >= 70 and resources >= 2
on-arrival: resources -= 2
unavailable-subtitle: [? if resources < 2: We do not have enough resources. ?] [? if z_relations < 70: They are not willing to be convinced ?]
go-to: negotiations_succeed 

@Braun_pressure
view-if: chancellor = "Braun"
choose-if: z_relations >= 60
unavailable-subtitle: [? if z_relations < 60: They are not willing to be convinced ?]
go-to: negotiations_succeed 

@Tried
go-to: support_labor_failed

@negotiations_succeed 
on-arrival: labor_strength += 5; labor_dissent -= 5; left_dissent -= 5; workers_spd += 5*(1-dissent); workers_z += 1; coalition_dissent += 1; z_relation -= 8; dvp_relation -= 4; dvp_right += 1; ddp_left += 1; ddp_cohesion -= 1; lvp_right += 1; goal_spd_cancel_peoples += 1; capital_strike_progress += 1 if dvp_relation <= 30; capital_strike_progress += 1 if unemployed > 15; capital_strike_progress += 1 if unemployed > 24; labor_goal_completed += 1; inflation += 0.1; economic_growth -= 0.2

Thanks to our negotiations with the <span style="color: #000000;">Center Party</span> and Joos, Stegerwald has agreed to support labor in their demands. However, many in the <span style="color: #000000;">Center Party</span> are annoyed by our meddling in the affairs of a ministry we ceded to them.

@support_labor_failed
on-arrival: labor_dissent += 5; left_dissent += 5; workers_spd -= 4; coalition_dissent += 1; dvp_relation -= 4; ddp_relation -= 2; lvp_relation -= 3; kpd_relation -= 4; workers_kpd += 3; capital_strike_progress += 1 if unemployed > 15; capital_strike_progress += 1 if unemployed > 24; dvp_right += 1; ddp_left += 0.5; ddp_cohesion -= 1; lvp_right += 1; goal_spd_cancel += 1; pro_labor -= 1; economic_growth -= 0.2

Labor Minister [+ labor_minister +] (**<span style="color: #000000;">**Z**</span>**) attempts at a compromise have fallen apart due to our agitation. He casts his lot in with the employers instead.

The blame for this episode is placed on our failed intervention, and serves as a bitter reminder to workers of our concession of the Labor Ministry to the bourgeois parties. There is deep resentment developing against the <span style="color: #c00000;">**SPD**</span>, especially among the more radical workers. The <span style="color: #700000;">**Communists**</span> stand to gain.

@support_employers
choose-if: in_grand_coalition = 1
subtitle: In order to appease the coalition, we will give Stegerwald free reign.
unavailable-subtitle: Stegerwald and his party want a fair compromise. He'd be confused if we were the ones urging him to the employers' side.
on-arrival: labor_dissent += 8; left_dissent += 8; workers_spd -= 6; coalition_dissent -= 1; dvp_relation += 5; z_relation += 5; ddp_relation += 4; lvp_relation += 5; coalition_dissent -= 1; workers_kpd += 5; kpd_coalition_dissent += 1; kpd_relation -= 8; capital_strike_progress -= 1 if capital_strike_progress > 1; capital_strike_progress -= 1 if capital_strike_progress > 8; dvp_left += 1; ddp_right += 0.5; lvp_left += 0.5; ddp_cohesion -= 1; goal_spd_cancel += 1; labor_goal_spd_peoples += 1; pro_labor -= 1

Seeking to appease the <span style="color: #C0A054;">**DVP**</span>, Stegerwald puts down the strikes. there is deep bitterness developing against the <span style="color: #c00000;">**SPD**</span> for letting this happen, especially among the more radical workers. The <span style="color: #700000;">**Communists**</span> stand to gain.

@balance
subtitle: This is Stegerwalds perfered solution to labor disputes
on-arrival: labor_dissent += 4; workers_spd -= 1; kpd_coalition_dissent += 1; kpd_relation -= 5; workers_kpd += 1; ddp_cohesion += 1; ddp_relation += 4; labor_goal_spd_peoples += 1

Labor Minister [+ labor_minister +] (**<span style="color: #000000;">**Z**</span>**) negotiates a compromise between both sides. We have assisted him in doing so.

Many workers are unhappy at our compromises, but they will learn to accept that we are all in a shared struggle against economic forces. This is perhaps the best we could do given the present circumstances.
